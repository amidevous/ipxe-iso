#!ipxe

# config

set URL_kickstart_centos7 ks=http://172.16.120.11/repo/ks.cfg
set reposerver http://172.16.120.10
set URL_initrd_centos7 ${reposerver}/centos/7/os/x86_64/images/pxeboot/initrd.img
set URL_vmlinuz_centos7 ${reposerver}/centos/7/os/x86_64/images/pxeboot/vmlinuz
set URL_installsource_centos7 ${reposerver}/centos/7/os/x86_64/
set IP_syslog_server 172.16.120.1

:menu

menu Select action

item kickstart_centos8 Kickstart Centos8 (ethernet)
item kickstart_centos7 Kickstart Centos7 (ethernet)
item ipxe_shell iPXE shell
item exit call exit (go to EFI setup)

choose --default kickstart_centos8 --timeout 5000 action && goto ${action} # wait 5 sec

:kickstart_centos8

set http_server_url http://172.16.120.10
set http_server2_url http://172.16.120.11
set image_path centos8/current/BaseOS/x86_64/os/images/pxeboot

set initrd_url ${http_server_url}/${image_path}/initrd.img
set vmlinuz_url ${http_server_url}/${image_path}/vmlinuz
set inst.repo_url ${http_server_url}/centos8/current/BaseOS/x86_64/os/
set ks_url ${http_server2_url}/repo/koios-ks/ks8.cfg

echo iPXE boot agent: Requesting IP from DHCP
dhcp
echo iPXE boot agent: attempting to start centos kickstart installation..
sleep 1

initrd ${initrd_url}
boot ${vmlinuz_url} inst.repo=${inst.repo_url} ${ks_url} initrd=initrd.img

:kickstart_centos7

# configure network

echo iPXE boot agent: Requesting IP from DHCP
dhcp
echo iPXE boot agent: attempting to start centos kickstart installation..
sleep 1

# load ramdisk, kernel and boot

# info available here:
# https://access.redhat.com/documentation/en-us/red_hat_enterprise_linux/7/html/installation_guide/chap-anaconda-boot-options
# https://docs.centos.org/en-US/8-docs/advanced-install/assembly_kickstart-and-advanced-boot-options/#debug-boot-options_kickstart-and-advanced-boot-options

initrd ${URL_initrd_centos7}
boot ${URL_vmlinuz_centos7} repo=${URL_installsource_centos7} ${URL_kickstart_centos7} initrd=initrd.img console=tty0 console=ttyS0,115200 inst.syslog=${IP_syslog_server}

:ipxe_shell

shell

:exit

exit
